1030 GOSUB 2000
1040 GOSUB 3000
1050 GOSUB 4000

2000 REM Title subroutine
2010 KEY OFF: CLS : WIDTH 80
2060 PRINT "Program 3-1: Find and Display Active Memory"
2100 PRINT
2999 RETURN

3000 REM Subroutine for the main search and display
3010 TRUE.% = -1
3020 FALSE.% = 0
3030 IN.MEMORY.% = FALSE.%
3040 FOR PARAGRAPH.! = 0 TO 65535! STEP 64
3050    GOSUB 5000
3060    IF (IN.MEMORY.% = FALSE.%) AND (MEMORY.HERE.% = TRUE.%) THEN GOSUB 6000
3070    IF (IN.MEMORY.% = TRUE.%) AND (MEMORY.HERE.% = FALSE.%) THEN GOSUB 7000
3080    IN.MEMORY.% = FALSE.%
3090    IF MEMORY.HERE.% THEN IN.MEMORY.% = TRUE.%
3100 NEXT PARAGRAPH.!
3999 RETURN

4000 REM Subroutine to finish up
4010 PRINT
4020 PRINT "Finished."
4999 SYSTEM

5000 REM Check for memory present -- sample the first 4 bytes
5010 DEF SEG = PARAGRAPH.!
5020 BYTE0.% = PEEK(0)
5030 BYTE1.% = PEEK(1)
5040 BYTE2.% = PEEK(2)
5050 BYTE3.% = PEEK(3)
5060 CHECK.COUNT.% = 0
5070 IF BYTE0.% = 255 THEN CHECK.COUNT.% = CHECK.COUNT.% + 1
5080 IF BYTE1.% = 255 THEN CHECK.COUNT.% = CHECK.COUNT.% + 1
5090 IF BYTE2.% = 255 THEN CHECK.COUNT.% = CHECK.COUNT.% + 1
5100 IF BYTE3.% = 255 THEN CHECK.COUNT.% = CHECK.COUNT.% + 1
5110 MEMORY.HERE.% = FALSE.%
5120 IF CHECK.COUNT.% <= 3 THEN MEMORY.HERE.% = TRUE.%
5999 RETURN

6000 REM Subroutine to start an active block of memory
6010 IN.MEMORY.% = TRUE.%
6020 START.! = PARAGRAPH.!
6999 RETURN

7000 REM Subroutine to end a block of active memory
7010 SIZE.! = (PARAGRAPH.! - START.!) * 16
7020 IF SIZE.! < 8 * 1024 THEN 7999 'Supress small-block false reports
7030 PRINT "Active memory beins at ";
7040 PRINT HEX$(START.!)
7050 PRINT " ends at ";
7060 PRINT HEX$(PARAGRAPH.! - 1);
7070 PRINT SIZE.! / 1024; "Kbytes"; " ("; SIZE.!; "bytes )"
7080 IN.MEMORY.% = FALSE.%
7090 PRINT
7999 RETURN

